apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def repo_context_url = hasProperty('artifactory_context_url') ? artifactory_context_url : System.getenv('artifactory_context_url')
def repo_username = hasProperty('artifactory_username') ? artifactory_username : System.getenv('artifactory_username')
def repo_password = hasProperty('artifactory_password') ? artifactory_password : System.getenv('artifactory_password')
def repo_key = hasProperty('artifactory_key') ? artifactory_key : System.getenv('artifactory_key')
def VERSION_NAME = System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "1.1.5"


android {
    compileSdkVersion versions.compileSdkVersion
    buildToolsVersion versions.buildToolsVersion

    defaultConfig {
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode = Integer.parseInt(VERSION_CODE)
        versionName = VERSION_NAME
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile libraries.appCompat
    compile libraries.googlePlayServices
}


def libraryGroupId = 'com.conouch.view'
def libraryArtifactId = 'location'
def libraryVersion = VERSION_NAME

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId

            artifact("$buildDir/outputs/aar/${project.name}-release.aar")
        }
    }
}

artifactory {
    contextUrl = repo_context_url
    publish {
        repository {
            repoKey = repo_key

            username = repo_username
            password = repo_password
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}